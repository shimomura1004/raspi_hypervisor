#include "../../../include/hypercall_type.h"

.globl get_el
get_el:
	mrs x0, CurrentEL
	lsr x0, x0, #2
	ret

.globl put32
put32:
	str w1,[x0]
	ret

.globl get32
get32:
	ldr w0,[x0]
	ret

.globl delay
delay:
	subs x0, x0, #1
	bne delay
	ret

.globl new_vm
new_vm:
    ldr x8, vm_args_p
    hvc #HYPERCALL_TYPE_CREATE_VM_FROM_ELF
	ret

.globl destroy_vm
destroy_vm:
	mov x8, x0
	hvc #HYPERCALL_TYPE_DESTOY_VM
	ret

.globl shutdown_hv
shutdown_hv:
	hvc #HYPERCALL_TYPE_SHUTDOWN_HV
	ret

.globl irq_vector_init
irq_vector_init:
	adr x0, vectors
	msr vbar_el1, x0
	ret

.globl enable_irq
enable_irq:
	msr daifclr, #2
	ret

.globl disable_irq
disable_irq:
	msr daifset, #2
	ret

.balign 2048
vectors:
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry

	.balign 128
	b invalid_entry
	.balign 128
	b handle_irq_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry

	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry

	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry
	.balign 128
	b invalid_entry

handle_irq_entry:
	// レジスタを退避 (x0-x30)
	sub sp, sp, #256
	stp x0, x1, [sp, #16 * 0]
	stp x2, x3, [sp, #16 * 1]
	stp x4, x5, [sp, #16 * 2]
	stp x6, x7, [sp, #16 * 3]
	stp x8, x9, [sp, #16 * 4]
	stp x10, x11, [sp, #16 * 5]
	stp x12, x13, [sp, #16 * 6]
	stp x14, x15, [sp, #16 * 7]
	stp x16, x17, [sp, #16 * 8]
	stp x18, x19, [sp, #16 * 9]
	stp x20, x21, [sp, #16 * 10]
	stp x22, x23, [sp, #16 * 11]
	stp x24, x25, [sp, #16 * 12]
	stp x26, x27, [sp, #16 * 13]
	stp x28, x29, [sp, #16 * 14]
	str x30, [sp, #16 * 15]

	bl handle_irq

	// レジスタを復帰
	ldr x30, [sp, #16 * 15]
	ldp x28, x29, [sp, #16 * 14]
	ldp x26, x27, [sp, #16 * 13]
	ldp x24, x25, [sp, #16 * 12]
	ldp x22, x23, [sp, #16 * 11]
	ldp x20, x21, [sp, #16 * 10]
	ldp x18, x19, [sp, #16 * 9]
	ldp x16, x17, [sp, #16 * 8]
	ldp x14, x15, [sp, #16 * 7]
	ldp x12, x13, [sp, #16 * 6]
	ldp x10, x11, [sp, #16 * 5]
	ldp x8, x9, [sp, #16 * 4]
	ldp x6, x7, [sp, #16 * 3]
	ldp x4, x5, [sp, #16 * 2]
	ldp x2, x3, [sp, #16 * 1]
	ldp x0, x1, [sp, #16 * 0]
	add sp, sp, #256
	eret

invalid_entry:
	b invalid_entry
